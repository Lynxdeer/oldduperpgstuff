on leftclick with wooden shovel:
	line 1 of player's tool's lore starts with "&7hippity hoppity you are now off my property"
	cancel event
	if lvl(player) is less than 20:
		send "&cYou must have at least combat 20 to use this item!"
		stop
	if "%region at player%" is "spawn in world ul_void":
		send "&cYou cannot use the shove staff here!"
		stop
	if difference between {-sscd::%player%} and now is less than 1 seconds:
		if {-sscd::%player%} is not set:
			exit 2 sections
		stop
	if {mana::%player%} is less than 3:
		send action bar "&cNot enough Mana!" to player
		stop
	modifyMana(player, 3)
	set {-sscd::%player%} to now
	play sound "minecraft:entity.iron_golem.attack" with volume 5 and pitch 1 at player for all players where [input's world is player's world]
	play sound "minecraft:entity.iron_golem.attack" with volume 5 and pitch 2 at player for all players where [input's world is player's world]
	play sound "minecraft:entity.iron_golem.attack" with volume 5 and pitch 0.5 at player for all players where [input's world is player's world]
	set {_loc::1} to location 1.6 meters above location of player
	set {_loc::2} to location 0.5 meters left of {_loc::1}
	set {_loc::3} to location 0.5 meters right of {_loc::1}
#
#  
#        
#
#
#        2         1         3
#
#
#        
#
# Spread Pattern
	set {_yw} to player's yaw 
	loop 25 times:
		loop {_loc::*}:
			set {_loc::%loop-index%}'s world to player's world
			{_dontshoot::%loop-index%} is not set
			loop blocks in radius 0.8 around {_loc::%loop-index%}:
				loop-block is solid
				set {_dontshoot::%loop-index%} to true
			{_dontshoot::%loop-index%} is not set
			
			set {_loc::2} to location 0.8 meters left of {_loc::2}
			set {_loc::3} to location 0.8 meters right of {_loc::3}
			set {-sscd::%player%} to now
			
			set {_ent::*} to hitbox(player, {_loc::%loop-index%})
			if size of {_ent::*} is more than 0: # Probably more efficient ways but who cares
			
				loop all entities where [{_ent::*} contains input]:
			
					if loop-entity is not a player:
					
						set {_v} to vector with yaw {_yw} and pitch 5
						push loop-entity {_v} with force 0.6
						push loop-entity upwards with force 0.5
					
					else: # Yes I could use stop but that breaks it for some reason
					
						loop-entity is not the player
						loop-entity's gamemode is survival
						loop-entity's world is not "ul_rpg"
						"%region at loop-entity%" does not start with "spawn"
						loop-entity is not flying
						set {_v} to vector with yaw {_yw} and pitch 5
						push loop-entity {_v} with force 0.5
						push loop-entity upwards with force 0.4
				
			set {_r} to random number between 1.5 and 2
			set {_loc::%loop-index%} to location {_r} meters in front of {_loc::%loop-index%}
			particle(sweep_attack, 2, 0.1, 0, 0.1, {_loc::%loop-index%})
			
		wait 1 tick
	set {-sscd::%player%} to now
	wait 20 ticks
	delete {-sscd::%player%}